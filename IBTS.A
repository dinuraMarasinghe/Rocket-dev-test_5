********************************************************************
* PROGRAM........IBTS.A
* DESCRIPTION....THIS PROGRAM IS THE MAIN CONTROLLING PROGRAM FOR
*                THE ENTRY OF INTER BRANCH TRANSFERS.  THIS PROGRAM
*                CALLS ALL THE MODULE PROGRAMS REQUIRED TO ENTER
*                AN INTER BRANCH TRANSFER ORDER, STARTING WITH ORDER
*                LINE DETAILS AND ENDING WITH THE SELECTION OF THE
*                NEXT OPTION.
*
* ENTRY..........POS MENU 1
* EXIT...........POS MENU 1
*
* PARAMETERS PASSED
* -----------------
* INPUT (COMING INTO)   : N/A
* OUTPUT (GOING OUT)    : N/A
********************************************************************
* REVISION HISTORY
*   Date    Inits  Job No   Reason
* --------  -----  ------   ------
* 13/12/90  ADG             REHASHED VERSION OF ACCOUNT SALE PROGRAM
* 01/02/91  KMEN            RE-REHASHED VERSION OF ACCOUNT SALE
*                           PROGRAM
* 24/05/91  LH     1155     GETPRICE.COMMON ADDED
* 06/09/91  JTB    1862     ADD DATE AND TIME CREATED ON
*                           ORDER-HEADER FILE.
* 01/11/91  IMT    2248     AMENDED TO ALLOW FOR IBT TYPE 2 BRANCHES.
* 25/02/92  IMT    2572     ADAPT FOR TIMBER EXTRAS.
* 28/09/92  TAB    2875     CHANGES TO 4 DIGIT TERMWORK.
* 21/06/93  JY     3531     Incorrect use of ERR subroutine.
* 11/08/93  AMS    3232     REMOVED WRITE.BRN.PRODUCT.MK2 AND ADDED
*                           THE INCLUDES WRITE.BRN.PRODUCT.V3 AND
*                           TRANS.BOUND.
* 26/7/94   PD     4295     RECOMPILED TO USE AMENDED WRITE.BRN.PRODUCT.V3
* 28/7/94   MATT   4318     ADDED INCLUDE POS.OPEN AND REMOVED DUPLICATE
*                           FILE OPENS (THEN PASSED IN COMMON BLOCK)
* 18/04/95  RAK    4736     Adjustments made for new screen layout.
* 03/11/95  MKENT  3916     Convert to use the new product files
* 06/12/95  MKENT  3916     Update POL.CUST.ORDER.NO
* 15/12/95  MKENT  3916     Change to use EQU.ORDER.REC.HEADER rather
*                           than EQU.OH.ORDER.HEADER.REC
* 16/01/96  DGD    5589     Move subroutine 'CHECK.ORDER.LINES' to end of prog to avoid overwriting of ORDER.HEADER
* 17/10/96  RAK    6537     Lock order header record after end trans.
* 06/02/97  MKENT  6669     Change POL.CUST.ORDER.NO fmt.
* 25/09/97  PD     7933     Remove use of brn-product
* 12/11/97  MMASK  6684     Change program to cater for "OLD" or "NEW" Deal System
* 15/11/00  EDERR  10538    Increase size of BRN.DETAIL.REC to 150.
* 17/11/00  marka  10518    IBT surcharge review
* 06/03/01  mpear  10618    Changed branding to be picked up from att<77> for Archers
* 09/07/01  EDERR  10734    Remove call to ORDLINE.IBTS.OLD and SCREENS IBT.LINE.V2
* 09/01/02  ctoug  10823    Prevent display of ticket no from overwriting
*                           video chars.
* 29/04/02  jwool  11098r2  Network ticket printing
* 03/09/02  jday   11201    Add arguments to BS.ADD.PO.LINE.USING.SALE
* 25/09/02  Les    11261    Fix READU problem.
* 17/09/04  bcorr  12098r8  Save last tickets used.
* 05/10/05  MMIMM  12366r9  Seagull changes
* 23/04/08  davep  13222    Change DIM BRN-DETAIL to 250
* 03/12/09  ckirb  14030.3  Writes to new IBT index
* 19/05/10  LMATH  14192r10 Integrate transport request creation to IBT
* 12/07/10  NKUMA  14192r14 Delivery Slot Restrictions in IBTS
* 20/12/11  SIDKU  14463r73 Nullify the screen variable for London
*                           Emission Zone.
* 02/03/12  RKARU  14793r13 Allow PTS branches to push IBT orders
*                           to KDS or POS PTS branches
* 30/10/12  vmuth  15004r1  To store mobile numbers
* 12/02/15  cmaud/ 15326r2  Set Order Sale Type
*           jaliv
* 17/05/23  phiel  15731r03 Update G ticket delivery indicator from M ticket
*
********************************************************************
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * THIS HAS BEEN SEAGULLISED ! ENSURE CHANGES ARE SEAGULLISED * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*      SEAGULL PANEL   : None
*      SEAGULL PROJECT : None
*********************************************************************
* NOTES; 27/11/95
* LINKING TO P.O.'S
* IF THE P.O. FILE FOR THE RECIPIENT BRANCH IS ON ANOTHER MACHINE, THE
* PROGRAM WILL STOP. WHEN THE TECHNIQUE FOR OPENING REMOTE FILES HAS
* BEEN FINALISED, IT WILL BE NECESSARY TO OPEN;
* (1) PO-LINE
* (2) PO-HEADER
* (3) PO-IDX
* PRIOR TO THE CALL TO BS.SELECT.PO.LINK
********************************************************************
*  I N I T I A L I S A T I O N
********************************************************************
*  DEFINE CONSTANTS AND EQUATES
*******************
$OPTIONS REALITY
$OPTIONS HEADER.DATE

      INCLUDE DG-INCLUDES TCSTANDARD
      INCLUDE DG-INCLUDES DEFFUN.INCLUDES
* 14793r13 RKARU - Start
      INCLUDE INCLUDES APP.DEFFUN.INCLUDES
* 14793r13 RKARU - End
      INCLUDE INCLUDES STANDARD.VARIABLES.7.0
      INCLUDE INCLUDES GETPRICE.COMMON
* MMIMM 12366r9 vv
      INCLUDE INCLUDES MN.TERMWORK.COMMON
      INCLUDE INCLUDES COMMON.SCREEN
* MMIMM 12366r9 end
      INCLUDE INCLUDES EQU.CURRENT.ORD
      INCLUDE INCLUDES EQU.ORDER.HEADER.DIM
      INCLUDE INCLUDES EQU.BRN.CONTROL
      INCLUDE INCLUDES EQU.PF.PRODUCT.MASTER.REC
      INCLUDE INCLUDES EQU.TERMWORK.REC
      INCLUDE INCLUDES EQU.PO.LINE.DIM
      INCLUDE INCLUDES EQU.BRN.DETAIL.REC
      INCLUDE INCLUDES EQU.ORDER.LINE.REC
*******************
*  OPENING FILES ROUTINE
*******************
      EQU TRUE TO 1
      EQU FALSE TO 0
      ERR = ""
      PROGRAM.NAME = "IBTS.A"
      DG.PROGRAM.NAME = PROGRAM.NAME
      ACCOUNT.NAME = OCONV(@WHO,'MCU')
      INCLUDE INCLUDES BS.OPEN.FILES
      INCLUDE INCLUDES OPEN.FILE.ACCT.ORD.XREF
      INCLUDE INCLUDES OPEN.FILE.BKUP.ORDERS
      INCLUDE INCLUDES OPEN.FILE.BRANCH.ZONE
      INCLUDE INCLUDES OPEN.FILE.CURRENT.ORD
      INCLUDE INCLUDES OPEN.FILE.ORD.TRACK
      INCLUDE INCLUDES OPEN.FILE.PO.LINE
* 6684 open of sy.params added
*     INCLUDE INCLUDES OPEN.FILE.SY.PARAMS
      IF ERR # "" THEN
         FATAL.ERROR.MSG = 'CANNOT OPEN FILES ':ERR
         TRANS.ACTION='A'
         DG.TRANS.FLAG=''
         GOSUB DG.TRANS.BOUND
         GOSUB FATAL.ERROR
      END
      INCLUDE INCLUDES CONSTRUCT.TOP.RIGHT
      MATREADU TERMWORK.REC FROM TERMWORK.FV, PORT ELSE
         FATAL.ERROR.MSG = "TERMWORK RECORD ":PORT:" DOES NOT EXIST"
         TRANS.ACTION='A'
         DG.TRANS.FLAG=''
         GOSUB DG.TRANS.BOUND
         GOSUB FATAL.ERROR
      END

      TERMINAL = PORT
      SCR.ERRMSG = ""

      READ SCREENS.REC FROM SCREENS.FV,"$IBT.LINE.V3" ELSE SCR.ERRMSG = "CANNOT READ SCREENS,$IBT.LINE.V3"

      IF SCR.ERRMSG THEN
         FATAL.ERROR.MSG = SCR.ERRMSG
         TRANS.ACTION="A"
         DG.TRANS.FLAG=''
         GOSUB DG.TRANS.BOUND
         GOSUB FATAL.ERROR
      END

      CALL FINMOD.FUNCTIONALITY("M.TICKET.DELIV.IND",SUB.OUT.DATA,SUB.ERRORS)
      COPY.M.TICKET.DELIV.IND = SUB.OUT.DATA<1>

* 23/04/08 davep  13222    Change DIM BRN-DETAIL to 250 Start
*      DIM BRN.DETAIL.REC(150)
      DIM BRN.DETAIL.REC(250)
* 23/04/08 davep  13222    Change DIM BRN-DETAIL to 250 End

* Determine the FEEDER status and local machine for this branch
      MATREAD BRN.DETAIL.REC FROM BRN.DETAIL.FV,TRM.BRN.CODE ELSE MAT BRN.DETAIL.REC = ""

** 14030.3 - 03/12/09 - ckirb - start
      THIS.IS.A.WAREHOUSE = FALSE
      IF BRN.WAREHOUSE.FLAG = "Y" THEN
         THIS.IS.A.WAREHOUSE = TRUE
      END
** 14030.3 - end

* 10122
*Set brand pricing method to current branch's brand
*10618 mpear - pick up branding from att<77> which is 'T' or 'K'
*      BRAND.PRICING.METHOD = BRN.BRAND[2,1]
      BRAND.PRICING.METHOD = BRN.PRICE.IND

      FEEDER.STATUS = BRN.FEEDER.STATUS
      LOCAL.MACH.ID = BRN.MACH.NME
* 6684 read deal flag
      READV CM.DEAL.SYSTEM FROM SY.PARAMS.FV, 'CM.DEAL.SYSTEM', 1 ELSE
         CM.DEAL.SYSTEM = 'OLD'
         WRITE CM.DEAL.SYSTEM ON SY.PARAMS.FV, 'CM.DEAL.SYSTEM'
      END
* 6684 use different clear prog. depending on deal flag
      IF CM.DEAL.SYSTEM = 'OLD' THEN
         CALL BS.CLEAR.GETPRICE
      END ELSE
         CALL CM.CLEAR.GETPRICE
      END
*
*******************
*  ERROR MESSAGES INITIALISATION
*  Set up NON-FATAL error messages in a Dimensioned Array.
*******************
      DIM ERR.MSG(20)
      MAT ERR.MSG = ""
      ERR.MSG(1) = "BRANCH CONTROL RECORD DOES NOT EXIST"
      ERR.MSG(2) = "SCREEN RECORD '$IBT.LINE' DOES NOT EXIST"
*******************
*  HELP MESSAGES INITIALISATION
*  Set up User PROMPT and HELP messages in a Dimensioned Array.
*******************
      DIM HELP.MSG(20)
      MAT HELP.MSG = ""
*******************
* INITIALISE SPECIFIC VARIABLES
*******************
      PROCESS.TYPE="" ; ORDER.NO=""
      BKUP.DATE=DATE()
      BKUP.DATE=BKUP.DATE:STR(":",6-LEN(BKUP.DATE))
      SPNUMBER = "" ; SALE.NO="" ; DELIV.NO=""
      DISP.SPNUMBER="" ; CUSTOMER.NAME="" ; CUST.NAME = CUSTOMER.NAME
      MAT ORDER.HEADER.REC="" ; ORDER.HEADER.REC.KEY=""
      POS.CUSTOMER.REC.KEY = ""
      MAT ORDER.LINE.REC="" ; ORDER.LINE.REC.KEY=""
      MAT PASS.TIMBER.LINES = ""
      MAT PF.PRODUCT.MASTER.REC = ""
      W.NULL = ""
      DEAL.PERCENTAGE = 0
*
      SALE.TYPE="M" ; DELIV.NO="0" ; TOTAL.GOOD.AMT=0 ; TOTAL.VAT.AMT=0
      INV.TOTAL=0
      TOTAL.BAL.DUE=0 ; LINE.NO=0 ; NUMBER.OF.LINES=0
      PO.CONVERTED = FALSE
* 6669 - remote PO link is always true
* We are no longer linking to PO's on the recipient machine.
*     REMOTE.PO.LINK = FALSE
      REMOTE.PO.LINK = TRUE
* MMIMM 12366r9 vv
      SEAGULL.USER = FALSE
      IF MN.TERMWORK.REC(17)<1,1> = "WJ" THEN
         SEAGULL.USER = TRUE
      END
* MMIMM 12366r9 end
* 14192r10 LMATH - Start
      TICKET.NUMBER = ''
      CURR.BRANCH = ''
      TARGET.BRANCH = ''
      FULFIL.BRANCH = ''
      DATE.CREATED = ''
      USER.ID = ''
      TRANSPORT.REQ.NUMBER = ''
* 14192r10 LMATH - End
* 14463r73 SIDKU - Start
      SC.CURR = ''
* 14463r73 SIDKU - End
*
*
*******************
*  MAIN BODY OF PROGRAM
*******************
      TRM.BRN.CODE = TRM.BRN.CODE "R%4"
      BRANCH.CODE = TRM.BRN.CODE
      MATREAD BRN.CONTROL.REC FROM BRN.CONTROL.FV,"MIC" ELSE
         FATAL.ERROR.MSG = ERR.MSG(2)
         TRANS.ACTION='A'
         DG.TRANS.FLAG=''
         GOSUB DG.TRANS.BOUND
         GOSUB FATAL.ERROR
      END
* 11098 jwool - network ticket printing
      CALL BS.DIST.CENTRE(BRANCH.CODE,SALE.TYPE,'','','','',1,'','','')
* 11098 jwool end
*
      GOSUB ACCOUNT.ORDER.HEADER         ; * ACCOUNT ORDER HEADER
*
100:            ***
      SPNUMBER=DELIV.NO:SALE.TYPE:TRM.BRN.CODE:SALE.NO
      CURRENT.ORD.REC.KEY=SPNUMBER
      MATREADU CURRENT.ORD.REC FROM CURRENT.ORD.FV,CURRENT.ORD.REC.KEY LOCKED CALL ERR("RECORD LOCKED BY ":STATUS(),"C") ; GOTO 100 ELSE NULL
      CURR.STATUS="LINE"
      MATWRITE CURRENT.ORD.REC ON CURRENT.ORD.FV,CURRENT.ORD.REC.KEY
***
      IF NOT(PO.CONVERTED) THEN GOSUB GET.ORDER.LINES        ; * GET ORDER LINES
***
200:            ***
      MATREADU CURRENT.ORD.REC FROM CURRENT.ORD.FV,CURRENT.ORD.REC.KEY LOCKED CALL ERR("RECORD LOCKED BY ":STATUS(),"C") ; GOTO 200 ELSE NULL
      CURR.STATUS="END"
      MATWRITE CURRENT.ORD.REC ON CURRENT.ORD.FV,CURRENT.ORD.REC.KEY
***
***
300:            ***
      MATREADU CURRENT.ORD.REC FROM CURRENT.ORD.FV,CURRENT.ORD.REC.KEY LOCKED CALL ERR("RECORD LOCKED BY ":STATUS(),"C") ; GOTO 300 ELSE NULL
      DELETE CURRENT.ORD.FV,CURRENT.ORD.REC.KEY
***
      GOSUB SELECT.NEXT.OPTION           ; * SELECT NEXT OPTION
      GOSUB CHECK.ORDER.LINES            ; ** moved ot avoid overwriting of ORDER.HEADER; * CHECK ORDER LINE DETAILS
***
      GOTO EXIT                          ; * GOTO FINAL END
**********************************************
*   SUBROUTINES AND EXCEPTIONS ROUTINES
**********************************************
*
ACCOUNT.ORDER.HEADER:    *  ACCOUNT SALE HEADER
      MAT ORDER.HEADER.REC=""
**
      GOSUB GET.ORDER.HEADER             ; * GET ACCOUNT ORDER HEADER
      TRANS.ACTION='S'
      DG.TRANS.FLAG=""
      GOSUB DG.TRANS.BOUND
      IF TRANS.STATUS THEN NULL ELSE
         FATAL.ERROR.MSG ="UNABLE TO START TRANSACTION IN ACCOUNT.ORDER.HEADER"
         GOSUB FATAL.ERROR
      END
**
      IF NOT(OH.ORD.DATE) THEN           ; * Header was not read
         TRANS.ACTION='A'
         DG.TRANS.FLAG=""
         GOSUB DG.TRANS.BOUND
         IF TRANS.STATUS THEN NULL ELSE
            FATAL.ERROR.MSG ="UNABLE TO ABORT TRANSACTION AT ACCOUNT.ORDER.HEADER "
            GOSUB FATAL.ERROR
         END
         STOP                            ; * EXIT PROGRAM
      END
**
      GOSUB GET.DOC.ISSUE                ; * GET DOC ISSUE NUMBER
**
      GOSUB POST.ORDER.HEADER            ; * POST ORDER HEADER
**
      IF REBUILD.FLG = "Y" THEN
         PROCESS.TYPE="A" ; ORDER.NO=ORDER.HEADER.REC.KEY
         INCLUDE INCLUDES SETUP.BKUP.ORDERS.TYPEA
         BKUP.ORDERS.KEY=PROCESS.TYPE:ORDER.NO:BKUP.DATE:TIME()
         WRITE BKUP.ORDERS.REC ON BKUP.ORDERS.FV,BKUP.ORDERS.KEY
      END
      TRANS.ACTION='E'
      DG.TRANS.FLAG=""
      GOSUB DG.TRANS.BOUND
*6537 Re-lock order header record.
      MATREADU ORDER.HEADER.REC FROM ORDER.HEADER.FV,ORDER.HEADER.REC.KEY ELSE MAT ORDER.HEADER.REC = ""
      IF TRANS.STATUS THEN NULL ELSE
         FATAL.ERROR.MSG ="UNABLE TO END TRANSACTION AT ACCOUNT.ORDER.HEADER "
         GOSUB FATAL.ERROR
      END
      RETURN
**
GET.ORDER.HEADER:    ** GET ACCOUNT ORDER HEADER **
      CALL BS.ORDER.ADJUSTMENT.HDR(DISP.SPNUMBER,MAT ORDER.HEADER.REC,"NEW","Inter Branch Transfer","","")
* 6669 - no longer need check for REMOTE.PO.LINK
* Now open the PO-LINE file for the selected site
*      IF CUST.CODE THEN
** DYNAMIC READ
*         READV RECIPIENT.MACHINE FROM BRN.DETAIL.FV,CUST.CODE,17 ELSE RECIPIENT.MACHINE = ""
*         IF RECIPIENT.MACHINE = LOCAL.MACH.ID THEN
*            REMOTE.PO.LINK = TRUE
** TO BE IMPLEMENTED...
** NOW OPEN PO-LINE, PO-HEADER, AND PO-IDX FILES
** ABORT IF ANY CANNOT BE OPENED
*         END
*      END
      RETURN
**
GET.DOC.ISSUE:    * GET DOC ISSUE NUMBER
* If BS.CONVERT.PO.TO.IBT has been called, DISP.SPNUMBER will be set
      IF DISP.SPNUMBER THEN
         SPNUMBER= DISP.SPNUMBER
         SALE.NO = SPNUMBER[7,4]
         PO.CONVERTED = TRUE
      END ELSE
         SALE.NO=""
         CALL GETDOC.M (SALE.TYPE,TRM.BRN.CODE,SALE.NO)
         SPNUMBER=DELIV.NO:SALE.TYPE:TRM.BRN.CODE:SALE.NO
      END
      RETURN
**
POST.ORDER.HEADER:    * POST ORDER HEADER
      ORDER.HEADER.REC.KEY=SPNUMBER
      AUTH.ID=TRM.AUTH.ID                ; * AUTH.ID  **
      LOCATION.NO=TRM.LOCATION.NO
* 14793r13 RKARU - Start
      READV SUPPLIER.BRAND FROM BRN.DETAIL.FV, CUST.CODE, 65 THEN
         SUPPLIER.BRAND = SUPPLIER.BRAND[2,1]
      END ELSE
         SUPPLIER.BRAND = ""
      END
      IF FUNC.BRAND.CHECK(BRN.BRAND[2,1],1,"","","","","") AND FUNC.BRAND.CHECK(SUPPLIER.BRAND,1,"","","","","") THEN
         OH.PTS.IBT.ACKNOWLEDGEMENT = "PUSH"
      END
* 14793r13 RKARU - End
**
*6537 lock order header record.
*      MATWRITE ORDER.HEADER.REC ON ORDER.HEADER.FV,ORDER.HEADER.REC.KEY
*15326r02 - set Order Sale Type
      OH.SALE.TYPE = "M"
*15326r02 End
      MATWRITEU ORDER.HEADER.REC ON ORDER.HEADER.FV,ORDER.HEADER.REC.KEY
      HEADER.KEY = ORDER.HEADER.REC.KEY
      GOSUB UPDATE.DELIVERY.INDICATOR

* 12098r8 17/09/04 bcorr -START- Save last tickets used.
      CALL BS.OE.LAST.TICKET.PROCESS('U',SPNUMBER[2,1]:SPNUMBER[7,4]:SPNUMBER[1,1],"","","","","","")
* 12098r8 17/09/04 bcorr -END- Save last tickets used.

      IF ORD.TRACK.FG = "Y" THEN
         ORD.TRACK.REC = ""
         ORD.TRACK.REC<2> = "0"
         ORD.TRACK.REC<1> = TRM.AUTH.ID
         ORD.TRACK.KEY = SPNUMBER:INT.TODAY:STR(":",6-LEN(INT.TODAY)):TIME()
         WRITE ORD.TRACK.REC ON ORD.TRACK.FV,ORD.TRACK.KEY
      END
350:            ***  WRITE CUSTOMER CROSS REFERENCE
      READU ACCT.ORD.XREF.REC FROM ACCT.ORD.XREF.FV,CUST.CODE LOCKED GOTO 350 ELSE ACCT.ORD.XREF.REC = ""
      INS SPNUMBER BEFORE ACCT.ORD.XREF.REC<1,-1>
      WRITE ACCT.ORD.XREF.REC ON ACCT.ORD.XREF.FV,CUST.CODE
*
      RETURN
**
GET.ORDER.LINES:    **  GET ORDER LINE
      SCRDISP = CLS:SCREENS.REC
      CRT SCRDISP
***
      TOKEN = ''
* MMIMM 12366r9 vv
      IF SEAGULL.USER THEN
         TLINE = @(1,0):"          ":@(27):"Inter Branch Transfer":TOP.RIGHT
      END ELSE
         TLINE = @(1,0):EXT.TODAY:@(27):"Inter Branch Transfer":TOP.RIGHT
      END
* MMIMM 12366r9 end
      TOKEN := TLINE
      DISP.SPNUMBER=SALE.TYPE:SALE.NO:DELIV.NO
* DYNAMIC READ
      READV SITE.NAME FROM BRN.DETAIL.FV,CUST.CODE,5 ELSE SITE.NAME = ""
* 10823 TOKEN := @(17,4):DISP.SPNUMBER:@(45,4):SITE.NAME
      TOKEN := @(18,4):DISP.SPNUMBER:@(45,4):SITE.NAME
      CRT TOKEN :
      LINE.NO=1 ; NOMORE=""
      TOTAL.GOOD.AMT=0 ; TOTAL.VAT.AMT=0 ; INV.TOTAL=0 ; TOTAL.BAL.DUE=0 ; NUMBER.OF.LINES=0
*
      LOOP UNTIL NOMORE="Y" DO
         GOSUB GET.ORDER.DETAIL          ; * GET ORDER LINE DETAIL
         IF LINE.NO="999" OR OL.DATE.CREATED = "" THEN NOMORE = "Y"
      REPEAT
      IF LINE.NO="" THEN LINE.NO=0
      RETURN
***
GET.ORDER.DETAIL:    ** GET ORDER LINE DETAIL
**
      TRANS.ACTION='S'
      DG.TRANS.FLAG=""
      GOSUB DG.TRANS.BOUND
      IF TRANS.STATUS THEN NULL ELSE
         FATAL.ERROR.MSG = "UNABLE TO START TRANSACTION AT GET.ORDER.DETAIL "
         GOSUB FATAL.ERROR
      END
***
      GOSUB GET.ORDLINE                  ; * GET ORDER LINE
***
      IF OL.DATE.CREATED="" THEN
         TRANS.ACTION='A'
         DG.TRANS.FLAG=""
         GOSUB DG.TRANS.BOUND
         IF TRANS.STATUS THEN NULL ELSE W.NULL=""
         LINE.NO=LINE.NO - 1
         IF LINE.NO < 0 THEN LINE.NO=0
         RETURN
      END
      GOSUB POST.ORDER.LINE              ; * POST ORDER LINE
**
      GOSUB POS.ORDER.HEADER             ; * POS ORDER HEADER
*
      IF REBUILD.FLG = "Y" THEN
         PROCESS.TYPE="B"
         ORDER.NO=ORDER.LINE.REC.KEY[4,99]
         BKUP.ORDERS.REC =""
         BKUP.ORDERS.KEY=PROCESS.TYPE:ORDER.NO:BKUP.DATE:TIME()
         MATBUILD ORDER.LINE.ITEM FROM ORDER.LINE.REC
         BKUP.ORDERS.REC<2> = ORDER.LINE.ITEM
         BKUP.ORDERS.REC<1>=LINE.NO
         WRITE BKUP.ORDERS.REC ON BKUP.ORDERS.FV,BKUP.ORDERS.KEY
      END
*
      LINE.NO = LINE.NO + 1
*
      TRANS.ACTION='E'
      DG.TRANS.FLAG=""
      GOSUB DG.TRANS.BOUND
      IF TRANS.STATUS THEN NULL ELSE
         FATAL.ERROR.MSG ="UNABLE TO END TRANSACTION AT GET.ORDER.DETAIL"
         GOSUB FATAL.ERROR
      END
*
*7933      GOSUB WRITE.BRN.PRODUCT
*
      RETURN
**
GET.ORDLINE:    * GET ORDER LINE
      LINE.NO = LINE.NO "R%3"
      SALE.NO = SALE.NO "R%4"
      CALL ORDLINE.IBTS(LINE.NO,DELIV.NO,TRM.BRN.CODE,SALE.NO,TLINE,CUST.CODE,FEEDER.STATUS,REASON.CODE,REMOTE.PO.LINK,MAT ORDER.HEADER.REC)
      RETURN
**
POST.ORDER.LINE:    * POST ORDER LINE
      LINE.NO = LINE.NO "R%3"
      SALE.NO = SALE.NO "R%4"
* 3916 - reset several fields
      OL.SUPP.LP = ""
      OL.SUPP.UOM = ""
* 3916
      ORDER.LINE.REC.KEY=LINE.NO:DELIV.NO:SALE.TYPE:TRM.BRN.CODE:SALE.NO
      MATWRITE ORDER.LINE.REC ON ORDER.LINE.FV,ORDER.LINE.REC.KEY
* Now update the POL.SALES.ATTACHED field in the PO-LINE record
* DYNAMIC READ - SORT OF - REFERRING TO ATTRIBUTE IN ORDER-LINE
      LINKED.PO.LINE = OL.PURCHASE.ORDER
      IF LINKED.PO.LINE THEN
* 8957 - create new PO header / line
         IF INDEX(OL.PURCHASE.ORDER,"NEW",1) THEN
            IF OCONV(OL.PURCHASE.ORDER,"G2*1") = "NEW" THEN
               CREATE.NEW.PO = TRUE
            END ELSE
               CREATE.NEW.PO = FALSE
            END
            CALL BS.ADD.PO.LINE.USING.SALE(ORDER.LINE.REC.KEY,OL.PURCHASE.ORDER,MAT ORDER.HEADER.REC,MAT ORDER.LINE.REC,SUCCESS,'','','','','','')
            IF CREATE.NEW.PO = TRUE THEN
               CRT SCRDISP:
               CRT TOKEN:
* 10823        CRT @(17,4):DISP.SPNUMBER:
               CRT.VAR = @(18,4):DISP.SPNUMBER ; CRT CRT.VAR:
            END
            IF NOT(SUCCESS) THEN
               OL.PURCHASE.ORDER = ''
            END ELSE
               IF CREATE.NEW.PO THEN
                  PO.STATE = "NEW"
               END ELSE
                  PO.STATE = "existing"
               END
               CALL ERR('Created Line ':OCONV(OL.PURCHASE.ORDER,'G0*1')+0:' on ':PO.STATE:' Purchase Order ': OCONV(OL.PURCHASE.ORDER,'G2*1'),'')
            END
            MATWRITE ORDER.LINE.REC ON ORDER.LINE.FV,ORDER.LINE.REC.KEY
         END ELSE
            MATREAD PO.LINE.REC FROM PO.LINE.FV,LINKED.PO.LINE THEN
               POL.SALES.ATTACHED = ORDER.LINE.REC.KEY
* 6669 - reformat POL.CUST.ORDER.NO.KEY
*           POL.CUST.ORDER.NO = LINE.NO"MR%3" : "*": ORDER.LINE.REC.KEY[2,1]:ORDER.LINE.REC.KEY[7,4]:ORDER.LINE.REC.KEY[1,1]
               POL.CUST.ORDER.NO = LINE.NO"MR%3": "*" : ORDER.LINE.REC.KEY[5,1]:ORDER.LINE.REC.KEY[10,4]:ORDER.LINE.REC.KEY[4,1]
* end 6669
               MATWRITE PO.LINE.REC ON PO.LINE.FV,LINKED.PO.LINE
            END
         END
      END

** 14030.3 - 03/12/09 - CKIRB - START
      IF THIS.IS.A.WAREHOUSE THEN
         CALL CS.MARS.WRITE.IBT.INDEX(ORDER.LINE.REC.KEY,
         MAT ORDER.HEADER.REC, MAT ORDER.LINE.REC,
         "", "", "", "")
      END
** 14030.3 - end

      FOR LOAD = 1 TO OL.TIMBER.LINES
         TE.KEY = ORDER.LINE.REC.KEY:"*":LOAD
         WRITE PASS.TIMBER.LINES(LOAD) ON TIMBER.LINE.FV,TE.KEY
      NEXT LOAD
* MATT TO AMEND SOON (13/7/93)
* STOP TYPE 3'S
*&&&
*CRT "DELIV.IND: ":ORDER.HEADER.REC<47>:EOL;INPUT DUMMY
      TOTAL.GOOD.AMT=TOTAL.GOOD.AMT + OL.ORD.GOOD.AMT        ; * ORD.GOOD.AMT
      TOTAL.VAT.AMT = TOTAL.VAT.AMT + OL.ORD.VAT.AMT         ; * ORD.VAT.AMT
      INV.TOTAL = TOTAL.GOOD.AMT + TOTAL.VAT.AMT
      TOTAL.BAL.DUE = INV.TOTAL
      NUMBER.OF.LINES = LINE.NO
      RETURN
***
POS.ORDER.HEADER:    ** UPDATE HEADER
      ORDER.HEADER.KEY=SPNUMBER
400:            **
* 11261 LES - fix READU problem
*      MATREADU ORDER.HEADER.REC FROM ORDER.HEADER.FV,ORDER.HEADER.KEY LOCKED CALL ERR("RECORD LOCKED BY ":STATUS():" - HIT <RTN> TO RETRY","") ; GOTO 400 ELSE MAT ORDER.HEADER.REC=""
      TOT.GOOD.AMT= OCONV(ICONV(TOTAL.GOOD.AMT, "MD2P" ), "MD2P" )
      TOT.VAT.AMT= OCONV(ICONV(TOTAL.VAT.AMT, "MD2P" ), "MD2P" )
      INV.TOT= OCONV(ICONV(INV.TOTAL, "MD2P" ), "MD2P" )
      TOT.BAL.DUE= OCONV(ICONV(TOTAL.BAL.DUE, "MD2P" ), "MD2P" )
      NO.OF.LINES=NUMBER.OF.LINES
      AUTH.ID=TRM.AUTH.ID                ; * AUTH.ID  **
      LOCATION.NO=TRM.LOCATION.NO
      OH.DATE.CREATED = DATE()           ; * DATE CREATED
      OH.TIME.CREATED = TIME()           ; * TIME CREATED
**
      MATWRITE ORDER.HEADER.REC ON ORDER.HEADER.FV,ORDER.HEADER.KEY
      HEADER.KEY = ORDER.HEADER.KEY
      GOSUB UPDATE.DELIVERY.INDICATOR
      RETURN
***
CHECK.ORDER.LINES:    *** CHECK ORDER LINE DETAILS
      TRANS.ACTION='S'
      DG.TRANS.FLAG=""
      GOSUB DG.TRANS.BOUND
      IF TRANS.STATUS THEN NULL ELSE
         FATAL.ERROR.MSG ="UNABLE TO START TRANSACTION AT CHECK.ORDER.LINES "
         GOSUB FATAL.ERROR
      END
*
      CALL CHECKLINE.M (SPNUMBER)
*
      TRANS.ACTION='E'
      DG.TRANS.FLAG=""
      GOSUB DG.TRANS.BOUND
      IF TRANS.STATUS THEN NULL ELSE
         FATAL.ERROR.MSG ="UNABLE TO END TRANSACTION AT CHECK.ORDER.LINES "
         GOSUB FATAL.ERROR
      END
* 15004r1 vmuth - Start
      IF CUST.CODE THEN
         NUMBER.ARRAY = OH.CUST.MOBILE<1,1>
         NAME.ARRAY = OH.CUST.MOBILE<1,2>

         IF OH.CUST.MOBILE.2<1,2> # '' THEN
            NUMBER.ARRAY<1,2> = OH.CUST.MOBILE.2<1,1>
            NAME.ARRAY<1,2> = OH.CUST.MOBILE.2<1,2>
         END

         EMAIL.ARRAY = OH.ALERT.EMAIL.ADDRESS

         IF NUMBER.ARRAY # '' OR EMAIL.ARRAY # "" THEN
            SMS.MODE = 'ADD'
            SMS.SUCCESS = ''
            SMS.ERR.MSG = ''

            CALL BS.CUST.DELIVERY.CONTACTS.MAINT(TRM.BRN.CODE,CUST.CODE,NUMBER.ARRAY,
            NAME.ARRAY,EMAIL.ARRAY,SMS.MODE,SMS.SUCCESS,SMS.ERR.MSG,'','','','','')
         END
      END
* 15004r1 vmuth - End
      RETURN
*
SELECT.NEXT.OPTION:    *** SELECT NEXT OPTION
      CALL IBTOPT.M (SALE.NO,DELIV.NO,TRM.BRN.CODE,MAT ORDER.HEADER.REC,TLINE,FEEDER.STATUS)
* 14192r14 NKUMA - Start
      DELIVERY.BRANCH = ''
      IF OH.TRANSPORT.BRANCH # "" THEN
         DELIVERY.BRANCH = OH.TRANSPORT.BRANCH
      END ELSE
         DELIVERY.BRANCH = TRM.BRN.CODE
      END
      IF DELIVERY.BRANCH AND DELIV.DATE THEN
         PARAMS.IN = DELIVERY.BRANCH
         PARAMS.IN<2> = DELIV.DATE<1,1>
         PARAMS.IN<3> = 'A'
         PARAMS.IN<4> = 'D'
         PARAMS.IN<5> = SPNUMBER
         PARAMS.IN<6> = OH.DELIVERY.SLOT
         CONTINUE.TICKET = FALSE
         LOOP UNTIL CONTINUE.TICKET DO
            PARAMS.OUT = ''
            XREF.ERROR = ''
            ENTRY.MODE = 'FILE.TICKET'
            CALL BS.REQ.DATE.XREF.MAINT(PARAMS.IN,PARAMS.OUT,XREF.ERROR,'','',ENTRY.MODE,PROGRAM.NAME,'','','','')
            IF FIELD(XREF.ERROR,'-',1) = 'LOCKED ' THEN
               CALL ERR(FIELD(XREF.ERROR,'-',2),'')
            END ELSE
               CONTINUE.TICKET = TRUE
            END
         REPEAT
* 14791r99 dhend start
      END ELSE
         XREF.ERROR = ''
* 14791r99 dhend end
      END
* 14192r14 NKUMA - End
* 14192r10 LMATH - Start
* Code for creation of transport request
* 14192r14 NKUMA - Start
*     IF BRN.BRANCH.DELIVERY.PARAMS<1,6> = 'Y' AND TRM.BRN.CODE # OH.TRANSPORT.BRANCH AND (DELIV.IND = "1" OR DELIV.IND = "3") THEN
      IF BRN.BRANCH.DELIVERY.PARAMS<1,6> = 'Y' AND TRM.BRN.CODE # OH.TRANSPORT.BRANCH AND (DELIV.IND = "1" OR DELIV.IND = "3") AND FIELD(XREF.ERROR,'-',1) # 'LOCKED ' THEN
* 14192r14 NKUMA - End
         TICKET.NUMBER = SPNUMBER
         CURR.BRANCH = TICKET.NUMBER[3,4]
         TARGET.BRANCH = CUST.CODE
         FULFIL.BRANCH = OH.TRANSPORT.BRANCH
         IF OH.DATE.CREATED = "" THEN
            DATE.CREATED = "UNKNOWN"
         END ELSE
            DATE.CREATED = OH.DATE.CREATED
         END
         USER.ID = TRM.AUTH.ID
         IF OH.TRANSPORT.BRANCH # '' THEN
* 14192r14 NKUMA - Start
            CONTINUE.TICKET = FALSE
            LOOP UNTIL CONTINUE.TICKET DO
               FATAL.ERROR.MSG = ''
* 14192r14 NKUMA - End
               CALL BS.DC.CREATE.TRANSPORT.REQUEST(TICKET.NUMBER,CURR.BRANCH,TARGET.BRANCH,FULFIL.BRANCH,DATE.CREATED,USER.ID,TRANSPORT.REQ.NUMBER,FATAL.ERROR.MSG,"","","","","")
               IF FATAL.ERROR.MSG # '' THEN
* 14192r14 NKUMA - Start
*                 CALL ERR("UNABLE TO CREATE THE TRANSPORT REQUEST TO BRANCH ":FULFIL.BRANCH:". PLEASE CONTACT HELP DESK ","")
                  IF FIELD(FATAL.ERROR.MSG,'*',1) = 'LOCKED ' THEN
                     CALL ERR(FIELD(FATAL.ERROR.MSG,'*',2),"")
                  END ELSE
                     CALL ERR("UNABLE TO CREATE THE TRANSPORT REQUEST TO BRANCH ":FULFIL.BRANCH:". PLEASE CONTACT HELP DESK ","")
                     CONTINUE.TICKET = TRUE
                  END
               END ELSE
                  CONTINUE.TICKET = TRUE
* 14192r14 NKUMA - End
               END
* 14192r14 NKUMA - Start
            REPEAT
* 14192r14 NKUMA - End
         END
      END
* 14192r10 LMATH - End
      RETURN

**************************
UPDATE.DELIVERY.INDICATOR:
**************************

      IF HEADER.KEY[2,1] = "M" AND COPY.M.TICKET.DELIV.IND AND OH.ORIGINAL.ORDER.HEADER THEN
         SUB.IN.DATA = "UPDATE.G.FROM.M"
         SUB.IN.DATA<2> = FIELD(OH.ORIGINAL.ORDER.HEADER,"-",1)
         SUB.IN.DATA<3> = FIELD(OH.ORIGINAL.ORDER.HEADER,"-",2)
         SUB.IN.DATA<4> = DELIV.IND
         CALL FINMOD.UPDATE.DELIVERY.INDICATOR(SUB.IN.DATA,SUB.OUT.DATA,SUB.ERRORS)
      END
      
      RETURN

*
EXIT:        ** FINAL EXIT
      STOP
*****
*7933      INCLUDE INCLUDES WRITE.BRN.PRODUCT.V3
      INCLUDE DG-INCLUDES DG.TRANS.BOUND
*****
**********************************************************************
FATAL.ERROR:
************
      DG.TRANS.FLAG=''
      GOSUB DG.TRANS.BOUND
      CRT CURSOR.ON:
      VAR = ''
      VAR<1,1> = "V1"
      VAR<1,2> = PROGRAM.NAME
      VAR<1,3> = DATE()
      VAR<1,4> = TIME()
      VAR<2> = FATAL.ERROR.MSG
      VAR<-1> = "!"
      DATA VAR
      CHAIN "SY.FATAL.ERROR"
      RETURN
**************************************************************************
      END
